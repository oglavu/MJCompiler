package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

:}


init with {:
	errorDetected = false;
:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, ENUM, CONST, READ, LENGTH, PRINT, RETURN, VOID, NEW;
terminal INC, PLUS, MUL, DIV, MOD, MINUS, DEC, ASSIGN, COLON, SEMI, COMMA, DOT;
terminal LPAREN, RPAREN, LBRACE, RBRACE, LBRACK, RBRACK;
terminal RELOP_EQ, RELOP_NEQ, RELOP_GT, RELOP_GE, RELOP_LT, RELOP_LE;
terminal String IDENT;
terminal Integer NUMBER, BOOL;
terminal Character CHARACTER;

nonterminal Program, ProgramName, ConVarEnumDecList, ConDecList, ConDecl, ConDeclMore, Constant, VarDeclList, VarDecl, VarDeclMore, EnumDecl, EnumElemList, EnumElem, EnumElemAssign, Type;
nonterminal MainMethod, StatementList, Statement, DesignatorStatement, Expr, AddopTermList, Term, MulopFactorList, Factor, Unary, FactorSub;
nonterminal Designator, DesignatorEnumElemName;
nonterminal Assignop, Addop, Mulop, Relop;

Program ::= (Program) PROG ProgramName ConVarEnumDecList LBRACE MainMethod RBRACE;

ProgramName ::= (ProgramName) IDENT;

ConVarEnumDecList ::= 	(ConVarEnumDecList_con) ConVarEnumDecList ConDecList
						|
						(ConVarEnumDecList_var) ConVarEnumDecList VarDeclList
						|
						(ConVarEnumDecList_enu) ConVarEnumDecList EnumDecl
						|
						(ConVarEnumDecList_e) /* epsilon */
						;
				
ConDecList ::= (ConDecList) CONST Type ConDecl ConDeclMore SEMI;

ConDecl ::= (ConDecl) IDENT ASSIGN Constant;

ConDeclMore ::= (ConDeclMore_comma) COMMA ConDecl ConDeclMore
				|
				(ConDeclMore_e) /* epsilon */
				;

Constant ::= 	(Constant_n) NUMBER
				|
				(Constant_c) CHARACTER
				|
				(Constant_b) BOOL
				;
				
VarDeclList ::= (VarDeclList) Type VarDecl VarDeclMore SEMI ;


VarDecl ::= (VarDecl_var) IDENT  
			|
			(VarDecl_array) IDENT LBRACK RBRACK
			; 


VarDeclMore ::= (VarDeclMore_comma) COMMA VarDecl VarDeclMore 
				|
				(VarDeclMore_e) /* epsilon */
				; 
				
EnumDecl ::= (EnumDecl) ENUM IDENT LBRACE EnumElemList RBRACE;

EnumElemList ::= 	(EnumElemList_rec) EnumElem EnumElemList
					|
					(EnumElemList_nxt) COMMA EnumElem EnumElemList
					|
					(EnumElemList_e) /* epsilon */
					;

EnumElem ::=	(DeclEnumElem_comma) IDENT EnumElemAssign
				;

EnumElemAssign ::=	(EnumElemAssign_assign) ASSIGN NUMBER
					|
					(EnumElemAssign_eps) /* epsilon */
					;

Type ::= (Type) IDENT;

MainMethod ::= (MainMethod) VOID IDENT LPAREN RPAREN VarDeclList LBRACE StatementList RBRACE;

StatementList ::= 	(StatementList_rec) StatementList Statement
				   	|										   
				  	(StatementList_e) /* epsilon */
				   	;

Statement ::=	(Statement_ds) DesignatorStatement SEMI
				|
				(Statement_return1) RETURN SEMI
		  		|
		  		(Statement_return2) RETURN Expr SEMI
			  	|
				(Statement_read) READ LPAREN Designator RPAREN SEMI
		  		|
		  		(Statement_print1) PRINT LPAREN Expr RPAREN SEMI
			  	|
				(Statement_print2) PRINT LPAREN Expr COMMA NUMBER RPAREN SEMI
		  		;
			     		
DesignatorStatement ::= (DesignatorStatement_assign) Designator Assignop Expr
						|
						(DesignatorStatement_error) error:e
						{: parser.report_error("Oporavak od greske u DS. Linija: " + eleft, null); :}
						|
						(DesignatorStatement_inc) Designator INC
						|
						(DesignatorStatement_dec) Designator DEC
						;
						
Expr ::= (Expr)  AddopTermList
		 ;
		 
AddopTermList ::= 	(AddopTermList_add) AddopTermList Addop Term
					|
					(AddopTermList_term) Term
					|
					(AddopTermList_relop) AddopTermList Relop Term
					;
   

Term ::= (Term) MulopFactorList
		 ;

MulopFactorList ::= (MulopFactorList_mul) MulopFactorList Mulop Factor
					|
					(MulopFactorList_factor) Factor
					;
			
Factor ::=	(Factor) Unary FactorSub
			;

Unary ::= 	(Unary_m) MINUS
			|
			(Unary_e) /* epsilon */
			;

FactorSub ::= 	(FactorSub_var) Designator
		   		|
		   		(FactorSub_n) NUMBER
		   		|
		   		(FactorSub_c) CHARACTER
		   		|
		   		(FactorSub_b) BOOL
		   		|		   
		   		(FactorSub_new_array) NEW Type LBRACK Expr RBRACK
		   		|
		   		(FactorSub_expr) LPAREN Expr RPAREN
		   		;
		   		
Designator ::= 	(Designator_var) IDENT
				|
				(Designator_length) Designator DOT LENGTH
				|
				(Designator_enum) Designator DOT DesignatorEnumElemName
				|
				(Designator_arr) Designator LBRACK Expr RBRACK
				;
				
DesignatorEnumElemName ::= (DesignatorEnumElemName) IDENT
				;

Assignop ::= (Assignop) ASSIGN;

Addop ::= (Addop_plus) PLUS
		  |
		  (Addop_minus) MINUS
		  ;

Mulop ::= (Mulop_mul) MUL
		  |		  
		  (Mulop_div) DIV
		  |
		  (Mulop_rem) MOD
		  ;
		  	  
Relop ::=	(Relop_eq) RELOP_EQ
			|
			(Relop_ne) RELOP_NEQ
			|
			(Relop_gt) RELOP_GT
			|
			(Relop_ge) RELOP_GE
			|
			(Relop_lt) RELOP_LT
			|
			(Relop_le) RELOP_LE
			;